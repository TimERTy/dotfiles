#
# A monochrome theme that displays basic information.
#
# Authors:
#   Brian Tse <briankftse@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/zLZNK.png
#

function +vi-git_status {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]='%F{red}●%f'
  fi
}

function prompt_timerty_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_timerty_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_timerty_precmd

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ':%F{yellow}%s%f'
  zstyle ':prezto:module:git:info:added' format ' %F{green}✚%f'
  zstyle ':prezto:module:git:info:ahead' format ' %F{yellow}⬆%f'
  zstyle ':prezto:module:git:info:behind' format ' %F{yellow}⬇%f'
  zstyle ':prezto:module:git:info:branch' format '%F{green}%b%f'
  zstyle ':prezto:module:git:info:commit' format ':%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format ' %F{red}✖%f'
  zstyle ':prezto:module:git:info:modified' format ' %F{red}?%f'
  zstyle ':prezto:module:git:info:position' format ':%F{red}%p%f'
  zstyle ':prezto:module:git:info:renamed' format ' %F{magenta}➜%f'
  zstyle ':prezto:module:git:info:stashed' format ' %F{cyan}✭%f'
  zstyle ':prezto:module:git:info:unmerged' format ' %F{yellow}═%f'
  zstyle ':prezto:module:git:info:untracked' format ' %F{white}◼%f'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' ' %F{082}(%f$(coalesce "%b" "%p" "%c")%s%F{082})%f' \
    'gitmods' '%C%D%A%B%S%a%d%m%r%U%u' \
    'rprompt' '${git_info[gitmods]}'


  # Define prompts.
  PROMPT='%F{015}%~${(e)git_info[prompt]}'$prompt_newline'%F{213}$ %f'
  RPROMPT='${(e)git_info[rprompt]}'

}

function prompt_timerty_preview {
  local +h PROMPT=''
  local +h RPROMPT=''
  local +h SPROMPT=''

  editor-info 2> /dev/null
  prompt_preview_theme 'timerty'
}

prompt_timerty_setup "$@"

